buildscript {
    repositories {
        mavenCentral()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.2'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 34

    defaultConfig {
      minSdkVersion 16
      targetSdkVersion 34
    }

    archivesBaseName = 'conceal'

    sourceSets {
      main {
        manifest.srcFile 'AndroidManifest.xml'
        java.srcDirs = ['java']
        jniLibs.srcDir 'libs'
        jni.srcDirs = []
      }
    }

    task ndkBuildRelease(type: Exec) {
      commandLine 'ndk-build', '-C', file('native/crypto/').absolutePath,
       'NDK_APPLICATION_MK=${PWD}/native/Application.mk',
       'NDK_MODULE_PATH=${PWD}/native/third-party/',
       'APP_BUILD_SCRIPT=${PWD}/native/crypto/Android.mk'
    }

    tasks.whenTaskAdded { task ->
        if (task.name == 'preBuild') {
            task.dependsOn ndkBuildRelease
        }
    }

    lintOptions {
      abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

version = "1.1.3.23-4kb"
group = "com.facebook.conceal"

afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                groupId group
                artifactId 'conceal'
                version version
                artifact "$project.buildDir/outputs/aar/${archivesBaseName}-release.aar"
            }
        }
    }
    tasks.getByName("publishToMavenLocal").mustRunAfter(tasks.getByName("assembleRelease"))
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}


